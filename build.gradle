plugins {
    id "com.jfrog.bintray" version "1.7.1"
}

description = "Use Webjars together with Tapestry"

group = "de.eddyson"
version = "1.0.0"

apply plugin: 'groovy'
apply plugin: 'maven'
apply plugin: 'jacoco'

def versions= [
  webjars_locator: '0.32',
  tapestry: '5.4.1',
  slf4j: '1.7.21',
  
  // test scopes
  codemirror: '5.13.2',
  babel: '6.14.0',
  spock: '1.0-groovy-2.4'
]

repositories {
  jcenter()
}

sourceCompatibility = '1.7'

task sourceJar(type: Jar) {
  dependsOn classes
  classifier "sources"
  from sourceSets.main.allSource
}

artifacts {
  archives sourceJar
}

dependencies {
  compile "org.slf4j:slf4j-api:$versions.slf4j"
  compile "org.apache.tapestry:tapestry-core:$versions.tapestry"
  compile "org.webjars:webjars-locator-core:$versions.webjars_locator"

  testCompile "javax.servlet:servlet-api:2.5"
  testCompile "org.spockframework:spock-tapestry:$versions.spock"
  testRuntime "org.webjars:codemirror:$versions.codemirror"
  testRuntime ("org.webjars.npm:babel-core:$versions.babel"){
    transitive = false
  }
  testRuntime "org.slf4j:slf4j-simple:$versions.slf4j"
}

bintray {
    user = 'fooberger'
    key = System.env.bintrayAPIKey
    configurations = ['archives']
    publish = true
    pkg {
        repo = 'maven'
        name = 'de.eddyson:tapestry-webjars'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/eddyson-de/tapestry-webjars'
    }
}

jar {
  manifest { attributes 'Tapestry-Module-Classes': 'de.eddyson.tapestry.webjars.WebjarsModule' }
}

check.dependsOn jacocoTestReport

jacocoTestReport {
    reports {
        xml.enabled true
    }
}
