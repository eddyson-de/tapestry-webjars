plugins {
  id "groovy"
  id "maven-publish"
  id "jacoco"
}

description = "Use Webjars together with Tapestry"
group = "de.eddyson"
version = "1.1.0"

repositories {
  mavenCentral()
}

sourceCompatibility = "11"

task sourceJar(type: Jar) {
  dependsOn classes
  classifier "sources"
  from sourceSets.main.allSource
}

publishing {
  publications {
    maven(MavenPublication) {
      from components.java
      artifact tasks.sourceJar
    }
  }
}

dependencies {
  implementation "org.slf4j:slf4j-api:1.7.32"
  implementation "org.apache.tapestry:tapestry-core:5.7.3"
  implementation "org.webjars:webjars-locator-core:0.47"

  testImplementation "org.apache.tapestry:tapestry-spock:5.7.3"

  testRuntimeOnly "org.slf4j:slf4j-simple:1.7.32"
  testImplementation "javax.servlet:javax.servlet-api:4.0.1"

  // used as example import libs (versions are hardcoded in tests)
  testRuntimeOnly "org.webjars:codemirror:5.13.2"
  testRuntimeOnly("org.webjars.npm:babel-core:6.14.0"){
    transitive = false
  }
}

jar {
  manifest { attributes 'Tapestry-Module-Classes': 'de.eddyson.tapestry.webjars.WebjarsModule' }
}

test {
  finalizedBy jacocoTestReport
}

jacocoTestReport {
  dependsOn test
  reports {
    html.required = true
    xml.required = true
  }
}
