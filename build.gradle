plugins {
    id "com.jfrog.bintray" version "1.4"
}

description = "Use Webjars together with Tapestry"

group = "de.eddyson"
version = "0.9.1"

apply plugin: 'groovy'
apply plugin: 'maven'

def versions= [
  webjars_locator: '0.29',
  tapestry: '5.4-rc-1',
  slf4j: '1.7.12',
  
  // test scopes
  codemirror: '5.7',
  babel: '6.0.20',
  spock: '1.0-groovy-2.4'
]

repositories {
  jcenter()
}

sourceCompatibility = '1.7'

task sourceJar(type: Jar) {
  dependsOn classes
  classifier "sources"
  from sourceSets.main.allSource
}

artifacts {
  archives sourceJar
}

dependencies {
  compile "org.slf4j:slf4j-api:$versions.slf4j"
  compile "org.apache.tapestry:tapestry-core:$versions.tapestry"
  compile "org.webjars:webjars-locator-core:$versions.webjars_locator"

  testCompile "javax.servlet:servlet-api:2.5"
  testCompile "org.spockframework:spock-tapestry:$versions.spock"
  testRuntime "org.webjars:codemirror:$versions.codemirror"
  testRuntime ("org.webjars.npm:babel-core:$versions.babel"){
    transitive = false
  }
  testRuntime "org.slf4j:slf4j-simple:$versions.slf4j"
}

bintray {
    user = project.properties.bintrayUser
    key = project.properties.bintrayAPIKey
    configurations = ['archives']
    publish = true
    pkg {
        repo = 'maven'
        name = 'de.eddyson:tapestry-webjars'
    }
}

jar {
  manifest { attributes 'Tapestry-Module-Classes': 'de.eddyson.tapestry.webjars.WebjarsModule' }
}
