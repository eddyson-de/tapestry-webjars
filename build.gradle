plugins {
  id "java-library"
  id "groovy"
  id "maven-publish"
  id "jacoco"
}

description = "Use Webjars together with Tapestry"
group = "de.eddyson"
version = "1.3.0"

repositories {
  mavenCentral()
}

java {
  sourceCompatibility = JavaVersion.VERSION_17
  targetCompatibility = JavaVersion.VERSION_17
}

task sourceJar(type: Jar) {
  dependsOn classes
  archiveClassifier = "sources"
  from sourceSets.main.allSource
}

publishing {
  publications {
    maven(MavenPublication) {
      from components.java
      artifact tasks.sourceJar
    }
  }
}

dependencies {
  implementation "org.slf4j:slf4j-api:2.0.13"
  implementation "org.apache.tapestry:tapestry-core-jakarta:5.9.0"
  api "org.webjars:webjars-locator-core:0.59"

  testImplementation "org.spockframework:spock-core:2.4-M5-groovy-3.0"
  testImplementation "org.apache.tapestry:tapestry-spock-jakarta:5.9.0"

  testRuntimeOnly "org.slf4j:slf4j-simple:2.0.13"
  testImplementation "jakarta.servlet:jakarta.servlet-api:6.1.0"

  // used as example import libs (versions are hardcoded in tests)
  testRuntimeOnly "org.webjars:codemirror:5.13.2"
  testRuntimeOnly("org.webjars.npm:babel-core:6.14.0"){
    transitive = false
  }
}

jar {
  manifest { attributes 'Tapestry-Module-Classes': 'de.eddyson.tapestry.webjars.WebjarsModule' }
}

test {
  useJUnitPlatform()
  finalizedBy jacocoTestReport
}

jacocoTestReport {
  dependsOn test
  reports {
    html.required = true
  }
}
