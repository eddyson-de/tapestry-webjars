plugins {
  id "java-library"
  id "groovy"
  id "maven-publish"
  id "jacoco"
}

description = "Use Webjars together with Tapestry"
group = "de.eddyson"
version = "1.2.0"

repositories {
  mavenCentral()
}

java {
  sourceCompatibility = JavaVersion.VERSION_11
  targetCompatibility = JavaVersion.VERSION_11
}

task sourceJar(type: Jar) {
  dependsOn classes
  classifier "sources"
  from sourceSets.main.allSource
}

publishing {
  publications {
    maven(MavenPublication) {
      from components.java
      artifact tasks.sourceJar
    }
  }
}

dependencies {
  implementation "org.slf4j:slf4j-api:1.7.36"
  implementation "org.apache.tapestry:tapestry-core:5.8.2"
  api "org.webjars:webjars-locator-core:0.52"

  testImplementation "org.spockframework:spock-core:2.1-groovy-3.0"
  testImplementation "org.apache.tapestry:tapestry-spock:5.8.2"

  testRuntimeOnly "org.slf4j:slf4j-simple:1.7.36"
  testImplementation "javax.servlet:javax.servlet-api:4.0.1"

  // used as example import libs (versions are hardcoded in tests)
  testRuntimeOnly "org.webjars:codemirror:5.13.2"
  testRuntimeOnly("org.webjars.npm:babel-core:6.14.0"){
    transitive = false
  }
}

jar {
  manifest { attributes 'Tapestry-Module-Classes': 'de.eddyson.tapestry.webjars.WebjarsModule' }
}

test {
  useJUnitPlatform()
  finalizedBy jacocoTestReport
}

jacocoTestReport {
  dependsOn test
  reports {
    html.required = true
  }
}
