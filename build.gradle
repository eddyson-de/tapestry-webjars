description = "Use Webjars together with Tapestry"

group = "de.eddyson"
version = "0.5.0"

apply plugin: 'groovy'
apply plugin: 'maven'

def versions= [
  webjars_locator: '0.21',
  tapestry: '5.4-beta-26',
  slf4j: '1.7.10',
  
  // test scopes
  codemirror: '5.0-1',
  spock: '1.0-groovy-2.4'
]

sourceCompatibility = '1.7'

task sourceJar(type: Jar) {
  dependsOn classes
  classifier "sources"
  from sourceSets.main.allSource
}

artifacts {
  archives sourceJar
}

dependencies {
  compile "org.slf4j:slf4j-api:$versions.slf4j"
  compile "org.apache.tapestry:tapestry-core:$versions.tapestry"
  compile ("org.webjars:webjars-locator:$versions.webjars_locator"){
    exclude group:'com.fasterxml.jackson.core', module: 'jackson-databind'
  }

  testCompile "javax.servlet:servlet-api:2.5"
  testCompile "org.spockframework:spock-tapestry:$versions.spock"
  testRuntime "org.webjars:codemirror:$versions.codemirror"
}

jar {
  manifest { attributes 'Tapestry-Module-Classes': 'de.eddyson.tapestry.webjars.WebjarsModule' }
}
